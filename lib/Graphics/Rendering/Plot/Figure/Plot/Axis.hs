-----------------------------------------------------------------------------
-- |
-- Module      :  Graphics.Rendering.Plot.Figure.Plot.Axis
-- Copyright   :  (c) A. V. H. McPhail 2010
-- License     :  BSD3
--
-- Maintainer  :  haskell.vivian.mcphail <at> gmail <dot> com
-- Stability   :  provisional
-- Portability :  portable
--
-- Axis
--
-----------------------------------------------------------------------------

module Graphics.Rendering.Plot.Figure.Plot.Axis (
                                                 Axis
                                                , AxisType(..),AxisSide(..),AxisPosn(..)
                                                , Tick(..), TickValues, GridLines
                                                , setTicks
                                                , setGridlines
                                                , setTickLabelFormat
                                                , setTickLabelTimeFormat
                                                , setTickLabelTimeFormat'
                                                , withAxisLabel
                                                , withAxisLine
                                                , withGridLine
                                                ) where

-----------------------------------------------------------------------------

import Control.Monad.State
import Control.Monad.Reader

import Graphics.Rendering.Plot.Types
import Graphics.Rendering.Plot.Defaults

import System.Locale

-----------------------------------------------------------------------------

changeLineType :: LineType -> AxisData -> AxisData
changeLineType lt ax = ax { _line_type = lt }

changeMinorTicks :: (Ticks -> Ticks) -> AxisData -> AxisData
changeMinorTicks t ax = ax { _minor_ticks = t (_minor_ticks ax) }

changeMajorTicks :: (Ticks -> Ticks) -> AxisData -> AxisData
changeMajorTicks t ax = ax { _major_ticks = t (_major_ticks ax) }

changeTickFormat :: TickFormat -> AxisData -> AxisData
changeTickFormat tf ax = ax { _tick_format = tf }

changeLabel :: (TextEntry -> TextEntry) -> AxisData -> AxisData
changeLabel f ax = ax { _label = f (_label ax) }

-----------------------------------------------------------------------------

-- | format the axis line
withAxisLine :: Line () -> Axis ()
withAxisLine m = do
                 l <- gets _line_type
                 lo <- asks _lineoptions
                 let lt = execLine m lo l
                 modify $ \s -> s { _line_type = lt }

-- | format the grid lines
withGridLine :: Tick -> Line () -> Axis ()
withGridLine t m = do
                 lo <- asks _lineoptions
                 (lt',v) <- case t of
                        Minor -> do
                               (Ticks lt'' v') <- gets _minor_ticks
                               return (lt'',v')
                        Major -> do
                               (Ticks lt'' v') <- gets _major_ticks
                               return (lt'',v')
                 let lt = execLine m lo lt'
                 case t of
                   Minor -> modify $ \s -> s { _minor_ticks = Ticks lt v }
                   Major -> modify $ \s -> s { _major_ticks = Ticks lt v }

-- | format the axis ticks
setTicks :: Tick -> TickValues -> Axis ()
setTicks Minor ts = modify $ \s -> changeMinorTicks (setTickValues ts) s
setTicks Major ts = modify $ \s -> changeMajorTicks (setTickValues ts) s

-- | should gridlines be displayed?
setGridlines :: Tick -> GridLines -> Axis ()
setGridlines Minor gl = modify $ \s -> changeMinorTicks (setTickGridlines (if gl then defaultGridLine else NoLine)) s
setGridlines Major gl = modify $ \s -> changeMajorTicks (setTickGridlines (if gl then defaultGridLine else NoLine)) s

-- | printf format that takes one argument, the tick value
setTickLabelFormat :: String -> Axis ()
setTickLabelFormat tf = modify $ \s -> changeTickFormat (FormatString tf) s

-- | Data.Time.Format.formatTime compatible string, assuming the axis is describing time in seconds since unix epoch
setTickLabelTimeFormat :: String -> Axis ()
setTickLabelTimeFormat tf = modify $ \s -> changeTickFormat (Time 0 1 defaultTimeLocale tf) s

-- | Data.Time.Format.formatTime compatible string, assuming the axis is describing time in seconds since unix epoch
-- | It also takes an offset and a multiplier, which are applied to the unix time before formatting
setTickLabelTimeFormat' :: String -> Double -> Double -> TimeLocale -> Axis ()
setTickLabelTimeFormat' tf offset mul locale = modify $ \s -> changeTickFormat (Time offset mul locale tf) s

-- | operate on the axis label
withAxisLabel :: Text () -> Axis ()
withAxisLabel m = do
                  ax <- get
                  to <- asks _textoptions
                  put $ ax { _label = execText m to (_label ax) } 

-----------------------------------------------------------------------------

